buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.0"
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/jannis/kotlin-pretty' }
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
}

apply plugin: 'kotlin-kapt'

group 'kparsec'
version '0.2.3'

def arrow_version = "0.10.5"
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    kapt "io.arrow-kt:arrow-meta:$arrow_version" // auto generation for @extension and @higherkind

    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation "io.arrow-kt:arrow-free:$arrow_version" // Trampoline for stacksafety tests
    implementation "io.arrow-kt:arrow-syntax:$arrow_version" // helper functions like curried...

    implementation "kotlin-pretty:kotlin-pretty:0.5.2" // pretty printing errors

    testImplementation("io.kotlintest:kotlintest-runner-junit5:3.4.2") {
        exclude group: "io.arrow-kt"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "BSD 3-Clause"
            url "https://github.com/1Jajen1/kparsec/blob/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jannis"
            name "Jannis Overesch"
            email "overesch.jannis@gmail.com"
        }
    }

    scm {
        url "https://github.com/1Jajen1/kparsec"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId 'kparsec'
            artifactId 'kparsec'
            version '0.2.3'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Kotlin functional parser combinators')
                root.appendNode('name', 'kparsec')
                root.appendNode('url', 'https://github.com/1Jajen1/kparsec')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    publish = true
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenPublication']
    pkg {
        repo = 'kparsec'
        name = 'kparsec'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/1Jajen1/kparsec'
        version {
            name = '0.2.3'
            released  = new Date()
        }
    }
}
